# author: Jhon Adrián Cerón-Guzmán
# email: <jacerong@unal.edu.co>
# version: 0.1
# description: generación de variantes secundarias a partir de la transcripción fonética de la palabra y recuperando entradas que suenan igual. Previo a esto, se realizan algunas modificaciones al grafema.

source lengthening_normalisation.foma

load phonology.bin
define PhoneticTranscription;

load es-dicc.bin
define StandardDicc;

# reemplazar convenciones fonémicas
define PhonemicConventions [%@ -> [a | o] || _ (s) .#.] .o.
                           [x -> [x | c h]] .o.
                           [q -> [q | q u] || _ [a | e | i | o]];

# recuperar el grafema "d" al final de palabras
define StandardizeEndings [a -> [a | {ada}] || .#. ?^>2 \a _ (s) .#.] .o.
                          [[..] -> (d) || .#. ?^>2 a _ [a | o] (s) .#.];

# conversión grafema/fonema
define G2Ph [LengtheningNormalisation .o. PhonemicConventions .o.
             StandardizeEndings .o. PhoneticTranscription];

# conversión fonema/grafema, con algunos cambios menores sobre el fonema
define Ph2G [x -> x ,, g -> g ,, "\u0263" -> "\u0263" ,,
             x -> g , x -> g w , x -> "\u0263" || _ [e | i | j] ,,
             g -> x , g -> g w || _ [e | i | j] ,,
             "\u0263" -> x , "\u0263" -> g w || _ [e | i | j] ,,
             "\u0263" u -> "\u0263" w , g u -> g w || [a | o]] .o.
            [j -> j ,, j -> i ,, i -> i , i -> j || _ [a | e | o | u]] .o.
            [w -> w ,, w -> u || _ [a | e | i | o]] .o.
            PhoneticTranscription.i;

regex G2Ph .o. Ph2G .o. StandardDicc;

save stack secondary_variants-Dicc.bin
